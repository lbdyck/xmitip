++USERMOD (SJ001A0) /*

       USAGE AFTER INSTALL IS TO UPDATE PROGXX
       TO ADD STATEMENT:
       EXIT ADD EXITNAME(IEFDB401) MODNAME(KFHDB401)

       NOTES: ===========================
       1.  CHANGE THE MODULE NAME FROM KFHDB401 TO SOMETHING
           MORE APPROPRIATE FOR YOUR SITE
           E.G. UNIVERSITY OF FLORIDA NERDC CALLS IT NERDB401
       2.  CHANGE THE FMID TO MATCH YOUR CURRENT JES2 RELEASE
           LEVEL.  THERE ARE JES2 MACRO DEPENDENCIES.
       3.  CHANGE THE USERMOD FROM SJ001A0 TO SOMETHING THAT
           CONFORMS TO YOUR LOCAL INSTALLATION STANDARDS.

       */.
++VER (Z038) FMID (HJE6605).
++JCLIN .
//LKED01   EXEC PGM=IEWL,
//         PARM=(LET,LIST,NCAL,RENT,
//         'AMODE=31','RMODE=ANY','AC=1')
//SYSLMOD   DD DSN=SYS1.LINKLIB,DISP=SHR
//SYSPUNCH  DD DSN=*.ASM01.SYSPUNCH,DISP=(SHR,PASS)
//SYSLIN    DD *
  INCLUDE SYSPUNCH(KFHDB401)
  NAME    KFHDB401(R)
/*
++SRC(KFHDB401) DISTLIB(SAMPLIB)  .
KFHDB401 TITLE 'DYNAMIC ALLOCATION INSTALLATION EXIT'
KFHDB401 AMODE 31
KFHDB401 RMODE ANY
         SPACE 10
***********************************************************************
* SEE THE OS/390 MVS INSTALLATION EXITS FOR MORE INFORMATION ON CODING
* AND USING IEFDB401.
*
* R1 POINTS TO PARAMETER LIST
*
*    +00 POINTER TO SVC 99 PARAMETER LIST
*    +04 POINTER TO ADDRESS OF WORKAREA (APPENDED TO SVC 99 PARM LIST)
*    +08 POINTER TO WORKAREA LENGTH
*    +0C POINTER TO JOBNAME
*    +10 POINTER TO PROGRAMMER NAME
*    +14 POINTER TO JOB ACCOUNTING INFORMATION
*    +18 POINTER TO STEP NAME
*    +1C POINTER TO PROGRAM NAME
*    +20 POINTER TO EXEC CARD ACCOUNTING INFORMATION.
*
* WARNING:  THIS ROUTINE DOES NOT GET A NEW SAVE AREA;
*    NO OPERATING SYSTEM CALLS ARE ALLOWED.
*    A SECTION OF THE THE SMTP PROCESSING CODE DOES USE A          NER
*    SAVE AREA IN THE WORKAREA PROVIDED BY THE EXIT.               NER
*
* FUNCTION:                                                        NER
*     DYNAMIC ALLOCATION EXIT                                      NER
*     FOR PGM=MVPMAIN (SMTP MAIL TASK), THE EXIT CHECKS FOR        NER
*         ALLOCATIONS WITH A DESTINATION NODE MATCHING THE         NER
*         JES NODE (FOR THE PRIMARY JES2 SUBSYSTEM) AND A          LBD
*         USERID, THEN SENDS AN SMTP MAIL TSO NOTIFY MESSAGE       NER
*         TO THE USERID VIA SVC 34.                                LBD
*     ** THE DSNUTILB IS NOT USED AT KAISER AND IS COMMENTED OUT   LBD
*     FOR PGM=DSNUTILB (DB2 UTILITY), THE EXIT ADDS THE            NER
*         JCL TAPE "DEFER" KEYWORD TO ALLOW TMS MOUNT MESSAGES     NER
*         TO BE DISPLAYED FOR DYNAMIC TAPE ALLOCATIONS.            NER
*                                                                  NER
* INSTALLATION:                                                    NER
*    TO USE EXIT FACILITY IN SYS1.PARMLIB(PROGXX), ADD THE LINE:   NER
*     EXIT ADD EXITNAME(IEFDB401)        MODNAME(KFHDB401)         LBD
*                                                                  NER
*    LINK AS RENT, REUS, AC(1) INTO A LINKLIST LIBRARY.            LBD
*                                                                  NER
* TESTING:                                                         NER
*    TO TEST USING DYNAMIC EXIT FACILTY:                           NER
*     SETPROG EXIT,DELETE,EX=IEFDB401,MOD=KFHDB401                 LBD
*     SETPROG EXIT,ADD,EX=IEFDB401,MOD=KFHDB401,DSN=SYS2.LINKLIB   LBD
*                                                                  NER
* MODIFICATIONS:                                                   NER
*   02/15/2000                                                     LBD
*     - ADD ADDRESS OF ROUTINE TO PGMLIST TABLE                    LBD
*     - COMMENT PROGRAM NAME DSNUTILB AND REMOVE DEFER CODE        LBD
*     - ADD CODE TO DYNAMICALLY PICK UP NJE NODE (JES2 ONLY) **    LBD
*     - CHANGE MESSAGE TEST TO SAY MAIL RECEIVED FROM.             LBD
*   12/07/99                                                       NER
*     ADD CODE TO SEND TSO NOTIFY MESSAGE FOR SMTP MAIL DELIVERY   NER
***********************************************************************
* THIS ROUTINE DEVELOPED AT THE UNIVERSITY OF FLORIDA NORTHEAST       *
* REGIONAL DATA CENTER (NERDC) AND MODIFIED BY KAISER PERMANENTE      *
* INFORMATION TECHNOLOGY.  *NO* WARRANTY ACCOMPANIES THIS CODE.       *
***********************************************************************
         SPACE 1
KFHDB401 CSECT
         SPACE 1
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING KFHDB401,R12
         SPACE 1
         L     R9,0(,R1)           SVC 99 REQUEST BLOCK
         USING S99RB,R9
         SPACE 1
         CLI   S99VERB,S99VRBAL    ALLOCATION REQUEST?
         BE    ISALLOC             YES, PROCESS IT
         SPACE 1
RETURN0  SR    R15,R15             CLEAR RETURN CODE
         SPACE 1
RETURN   L     R14,12(,R13)        RESTORE
         LM    R0,R12,20(R13)        CALLER'S REGS
         BR    R14                     AND RETURN
         EJECT
ISALLOC  L     R2,X'1C'(,R1)       POINTER TO PROGRAM NAME
         LA    R15,PGMLIST-12      POINT TO LIST OF PROGRAM NAMES
         SPACE 1
TSTPGMLP LA    R15,12(,R15)        BUMP TO NEXT PGM NAME
         CLI   0(R15),X'40'        END OF LIST?
         BE    RETURN0             YES, SKIP ALL THIS
         CLC   0(8,R15),0(R2)      MATCH?
         BNE   TSTPGMLP            ELSE CONTINUE LOOKING
         L     R15,8(R15)         LOAD POINTER TO ROUTINE          LBD
         BR    R15                AND GO THERE                     LBD
*
*        SMTP PROCESSING ROUTINE                                   LBD
*                                                                  NER
         SPACE 1                                                   NER
DOSMTP   L     R10,4(,R1)          LOAD DB401 WORKAREA PTR         NER
         L     R10,0(,R10)         LOAD DB401 WORKAREA ADDR        NER
         USING WORKAREA,R10        SET WORKAREA ADDRESSING         NER
         L     R15,8(,R1)          LOAD DB401 WORKAREA LENGTH      NER
         C     R15,=A(WORKLEN)     CHECK LENGTH OF WORKAREA        NER
         BL    RETURN0             INSUFFICIENT WORKAREA, RETURN   NER
         SPACE 1                                                   NER
         MVC   NODE,=CL8' '        INIT NODE                       NER
         MVC   USERID,=CL8' '      INIT USERID                     NER
         SPACE 1                                                   NER
         L     R8,S99TXTPP         GET TEXT UNIT POINTER LIST ADDR NER
         USING S99TUPL,R8          SET ADDRESSING TO TUPL          NER
         SPACE 1                                                   NER
CKDLP    L     R7,S99TUPTR         GET TEXT UNIT ADDR              NER
         USING S99TUNIT,R7         SET TEXT UNIT ADDRESSING        NER
         LA    R7,0(,R7)           CLEAR HIGH ORDER LAST TU BIT    NER
         LTR   R7,R7               CHECK FOR EMPTY TU              NER
         BZ    CKDLPE              EMPTY TU, SKIP CHECK            NER
         CLC   S99TUNUM,=H'1'      CHECK UNIT COUNT                NER
         BNE   CKDLPE              NOT 1, CHECK NEXT TEXT UNIT     NER
         LH    R15,S99TULNG        LOAD TEXT UNIT LENGTH           NER
         CH    R15,=H'1'           CHECK CHECK UNIT LENGTH         NER
         BL    CKDLPE              LENGTH INVALID, CHECK NEXT TU   NER
         CH    R15,=H'8'           CHECK TEXT UNIT LENGTH          NER
         BH    CKDLPE              LENGTH TOO HIGH, CHECK NEXT TU  NER
         BCTR  R15,0               DECREMENT LENGTH FOR EX         NER
         CLC   S99TUKEY,=AL2(DALSUSER)   CHECK FOR DEST NODE       NER
         BNE   CKU                 NOT NODE, CHECK USERID          NER
         B     *+10                SKIP EX                         NER
         MVC   NODE(*-*),S99TUPAR   EX                             NER
         EX    R15,*-6             SET NODE                        NER
         B     CKDLPE              CHECK NEXT TEXT UNIT            NER
CKU      CLC   S99TUKEY,=AL2(DALUSRID)   CHECK FOR DEST USERID     NER
         BNE   CKDLPE              NOT NODE, CHECK USERID          NER
         B     *+10                SKIP EX                         NER
         MVC   USERID(*-*),S99TUPAR EX                             NER
         EX    R15,*-6             SET USERID                      NER
         CLI   USERID+7,C' '       CHECK USERID LENGTH             NER
         BNE   RETURN0             USERID TOO LONG FOR SEND, EXIT  NER
         SPACE 1                                                   NER
CKDLPE   TM    S99TUPTR,S99TUPLN   CHECK FOR LAST TEXT UNIT        NER
         BO    CKDEST              LAST TEXT UNIT, CHECK DEST      NER
         LA    R8,4(,R8)           POINT TO NEXT TEXT UNIT         NER
         B     CKDLP               CHECK NEXT TEXT UNIT            NER
         SPACE 1                                                   NER
CKDEST   CLC   NODE,=CL8' '        CHECK FOR NO NODE               NER
         BE    CKUSR               NO NODE, CONTINUE               NER
*----------------------------------------------------------------- LBD
         L     R6,CVTPTR           -> CVT                          LBD
         USING CVT,R6                                              LBD
         L     R6,CVTJESCT         -> JESCT                        LBD
         DROP  R6
         USING JESCT,R6                                            LBD
         L     R6,JESSSCT          -> SSCT FOR JES2 (PRIMARY SSN)  LBD
         DROP  R6                                                  LBD
         USING SSCT,R6                                             LBD
         L     R6,SSCTSUS2                                         LBD
         DROP  R6                                                  LBD
         USING HCCT,R6                                             LBD
         CLC   NODE(8),CCTNDENM   COMPARE FOR SYSTEM JES2 NODE     LBD
         DROP  R6                                                  LBD
*----------------------------------------------------------------- LBD
         BNE   RETURN0             NOT SYSTEM NODE, RETURN         NER
CKUSR    CLC   USERID,=CL8' '      CHECK USERID                    NER
         BE    RETURN0             NO USERID, RETURN               NER
         SPACE 1                                                   NER
         MVC   MSG(MSGLN),MSGL     SET MESSAGE PARM                NER
         MVC   MSG+67(8),USERID    SET USERID IN SEND MSG          NER
         LA    R14,MSG+67          POINT TO END OF USERID          NER
EUSRLP   CLI   0(R14),C' '         CHECK FOR END OF USERID         NER
         BE    EUUSR               BLANK, MUST BE END              NER
         LA    R14,1(R14)          ELSE POINT TO NEXT CHAR         NER
         B     EUSRLP              CHECK NEXT CHAR                 NER
EUUSR    MVI   0(R14),C')'         CLOSE ) FOR SEND USERID         NER
         L     R14,12(,R1)         LOAD DB401 JOBNAME PTR          NER
         MVC   MSG+45(8),0(R14)    SET SMTP JOBNAME IN SEND MSG    NER
         SPACE 1                                                   NER
         LR    R1,R13              COPY OLD SAVE AREA ADDR         NER
         LA    R13,SAVEAREA        LOAD ADDR OF NEW SAVE AREA      NER
         ST    R1,SAVEAREA+4       SAVE OLD SAVE AREA ADDR IN NEW  NER
         ST    R13,8(,R1)          SAVE NEW SAVE AREA ADDR IN OLD  NER
         SPACE 1                                                   NER
         XC    TMEDTE,TMEDTE       INIT TIME AND DATE              NER
         TIME  DEC,TMEDTE,ZONE=LT,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM,    +
               MF=(E,TIME)                                         NER
         UNPK  TDWORK(7),TME(4)    CONVERT TIME TO EBCDIC          NER
         MVC   MSG+8(2),TDWORK     SET TIME HH IN SEND MSG         NER
         MVC   MSG+11(2),TDWORK+2  SET TIME MM IN SEND MSG         NER
         MVC   MSG+14(2),TDWORK+4  SET TIME SS IN SEND MSG         NER
         UNPK  TDWORK,DTE(5)       CONVERT DATE TO EBCDIC          NER
         MVC   MSG+17(2),TDWORK    SET DATE MONTH IN SEND MSG      NER
         MVC   MSG+20(2),TDWORK+2  SET DATE DAY IN SEND MSG        NER
         MVC   MSG+23(2),TDWORK+6  SET DATE YEAR IN SEND MSG       NER
         SPACE 1                                                   NER
         MODESET KEY=ZERO          SET KEY 0                       NER
         SR    R0,R0               SETUP FOR SEND                  NER
         LA    R1,MSG              POINT TO SEND MSG               NER
         SVC   34                  ISSUE THE SEND COMMAND          NER
         MODESET KEY=NZERO         SET PROBLEM KEY                 NER
         SPACE 1                                                   NER
         L     R13,SAVEAREA+4      LOAD ADDR OF OLD SAVE AREA      NER
         B     RETURN0             EXIT                            NER
         SPACE 5                                                   NER
CHKDEFER DS    0H                                                  NER
         SPACE 1
*
*        NOW WE THINK THAT THIS IS ONE OF THE PROGRAMS THAT HAS
*        BEEN DOING DYNAMIC ALLOCATION OF TAPE DRIVES WITHOUT
*        SPECIFYING "DEFER".
*
         SPACE 1
         L     R10,4(,R1)          ADDRESS OF WORKAREA
         L     R10,0(,R10)         WORKAREA
         SPACE 1
         L     R8,S99TXTPP         TEXT UNIT POINTER LIST
         USING S99TUPL,R8
         SPACE 1
         S     R8,=F'4'            BACK UP POINTER
         SPACE 1
TESTPTR  LA    R8,4(,R8)           BUMP TO NEXT TU POINTER
         TM    S99TUPTR,X'80'      LAST TEXT UNIT POINTER?
         BNO   TESTPTR             NO, KEEP LOOKING
         SPACE 1
         LA    R15,4(,R8)          SHOULD POINT TO WORK AREA
         SR    R15,R10             SHOULD MATCH
         BZ    SETDEFER            YES,
         SPACE 1
         DC    X'00DEAD01'         OOPS
         SPACE 1
SETDEFER NI    S99TUPTR,255-X'80'  CLEAR LAST FLAG
         LA    R8,4(,R8)           BUMP TO NEXT SLOT
         LA    R10,4(,R10)         BUMP FOR TEXT UNIT AREA
         ST    R10,S99TUPTR        SET IN LIST
         OI    S99TUPTR,X'80'      FLAG AS LAST ENTRY
         MVC   0(L'DEFERKEY,R10),DEFERKEY  SET DEFER TOKEN
         SPACE 1
         B     RETURN0             AND RETURN
         SPACE 3
PGMLIST  DS    0D
SMTPPGM  DC    CL8'MVPMAIN',A(DOSMTP) SMTP ROUTINE                 LBD
**       DC    CL8'DSNUTILB',A(CHKDEFER) DB2 RECOVERY UTILITY      LBD
         DC    CL8' ',A(0)         SPACE FOR MORE ENTRIES          LBD
         DC    CL8' END END',A(0)  END OF LIST                     LBD
         SPACE 3
DEFERKY  DC    AL2(DALDEFER),AL2(0,0)
DEFERKEY EQU   DEFERKY,*-DEFERKY
         SPACE 3
         DS    0F                  SVC 34 SEND COMMAND MODEL       NER
MSGL     WTO   'SE ''HH.MM.SS MM/DD/YY MAIL RECEIVED FROM JOBNAMEJ'',LO+
               GON,USER=(USERIDX ',MF=L                      NER
         SPACE 10                                                  NER
         LTORG ,
         EJECT
         SPACE 10                                                  NER
*  WORKAREA                                                        NER
WORKAREA DSECT ,                   DB401 WORKAREA                  NER
SAVEAREA DS    18F                 SAVE AREA                       NER
         SPACE ,                                                   NER
NODE     DS    CL8                 SMTP MAIL FILE DEST NODE        NER
USERID   DS    CL8                 SMTP MAIL FILE DEST USERID      NER
         SPACE ,                                                   NER
TMEDTE   DS    0XL16               TIME AND DATE FROM SYSTEM       NER
TME      DS    XL8                 TIME (HHMMSSTHMIJU)             NER
DTE      DS    XL4                 DATE (MMDDYYYY)                 NER
         DS    XL4                 (RESERVED)                      NER
         SPACE ,                                                   NER
TDWORK   DS    XL9                 DATE WORK AREA                  NER
         SPACE ,                                                   NER
TIME     TIME  LINKAGE=SYSTEM,MF=L                                 NER
         SPACE ,                                                   NER
         DS    0F                  SVC 34 SEND COMMAND             NER
MSG      WTO   'SE ''HH.MM.SS MM/DD/YY MAIL RECEIVED FROM JOBNAMEJ'',LO+
               GON,USER=(USERIDX ',MF=L                      NER
MSGLN    EQU   *-MSG                                               NER
         SPACE ,                                                   NER
WORKLEN  EQU   *-WORKAREA          WORKAREA LENGTH                 NER
         SPACE 10                                                  NER
*  MAPPING MACROS                                                  NER
         SPACE 1                                                   NER
         IEFZB4D0 ,
         EJECT
         IEFZB4D2 ,
         EJECT
         CVT   DSECT=YES
         EJECT
         IEFJESCT
         EJECT
         IEFJSCVT
         EJECT
         $HASPEQU
         EJECT
         $HCCT
         EJECT
         $HFAME
         EJECT
         $SCAT
         EJECT
         $XECB
         SPACE 3
         END   KFHDB401
